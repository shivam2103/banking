plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'org.asciidoctor.convert' version '1.5.8'
	id 'java'
	id 'org.liquibase.gradle' version '2.0.4'
}

group = 'com.shivam'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	// Swagger
	implementation("io.springfox:springfox-boot-starter:3.0.0")
	// Liquibase
	implementation 'org.liquibase:liquibase-core'
	liquibaseRuntime 'org.liquibase:liquibase-core'
	liquibaseRuntime("org.postgresql:postgresql")
	liquibaseRuntime("org.liquibase.ext:liquibase-hibernate5:4.3.5")
	liquibaseRuntime sourceSets.main.output
	liquibaseRuntime sourceSets.main.runtimeClasspath
	liquibaseRuntime sourceSets.main.compileClasspath
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

// Liquibase Configurations
diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava
def timestamp = new Date().getTime()
def diffChangeLog = "src/main/resources/db/changelog/generated/${timestamp}_changelog.xml"
liquibase {
	activities {
		main {
			driver "org.postgresql.Driver"
			url 'jdbc:postgresql://localhost/banking_application'
			username 'banking'
			password 'ba nking'
			changeLogFile diffChangeLog
			referenceUrl 'hibernate:spring:com.shivam.banking' +
					'?dialect=org.hibernate.dialect.PostgreSQL9Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy' +
					'&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
		}
	}
}
